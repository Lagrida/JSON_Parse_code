
JSON_text 			::= ws value ws
begin_array 		::= ws "[" ws
begin_object 		::= ws "{" ws
end_array 			::= ws "]" ws
end_object 			::= ws "}" ws
name_separator 	::= ws ":" ws
value_separator ::= ws "," ws
value 					::= false | null | true | object | array | number | string
object 					::= begin_object (member (value_separator member)*)? end_object
member 					::= string name_separator value
array 					::= begin_array (value (value_separator value)*)? end_array
number 					::= minus? int frac? exp?
ws 							::= [\t\n\r]*
false 					::= "false"
null  					::= "null"
true  					::= "true"
decimal_point 	::= "."
digit1_9 				::= [1-9]
e 							::= [eE]
exp 						::= e (minus | plus)? DIGIT+
frac 						::= decimal_point DIGIT+
int 						::= zero | (digit1_9 DIGIT*)
minus 					::= "-"
plus 						::= "+"
zero 						::= "0"
string 					::= quotation_mark char* quotation_mark
char 						::= unescaped | escape ('"' | "\\" | "/" | "b" | "f" | "n" | "r"  | "t" | "u" HEXDIG HEXDIG HEXDIG HEXDIG)
escape 					::= "\\"
quotation_mark 	::= '"'
unescaped 			::= [^\0-\x1F\x22\x5C]
DIGIT  					::= [0-9]
HEXDIG 					::= [0-9a-f]i
